``` abap
*&---------------------------------------------------------------------*
*& Include          ZC102FIR0017F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Module  EXIT_100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE exit_100 INPUT.

  CALL METHOD : go_right_alv_grid->free,
                go_tree->free,
                go_right_bot_cont->free,
                go_left_cont->free,
                go_right_cont->free,
                go_split_cont->free,
                go_container->free.

  FREE : go_right_bot_cont, go_right_alv_grid,
         go_tree, go_left_cont, go_right_cont,
         go_split_cont, go_container.

  LEAVE TO SCREEN 0.

ENDMODULE.
*&---------------------------------------------------------------------*
*& Module STATUS_0100 OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
MODULE status_0100 OUTPUT.
  SET PF-STATUS 'MENU100'.
  SET TITLEBAR 'TITLE100'.
ENDMODULE.
*&---------------------------------------------------------------------*
*& Form display_screen_100
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen_100 .

  IF go_container IS NOT BOUND.

    CLEAR : gt_fcat, gs_fcat.
    PERFORM set_field_catalog USING : 'X' 'SERNO'   'ZC102FIT0014' 'C' ' ',
                                      'X' 'PARTNER' 'ZC102FIT0014' 'C' ' ',
                                      'X' 'DWDATE'  'ZC102FIT0014' 'C' ' ',
                                      ' ' 'WRBTR'   'ZC102FIT0014' ' ' ' ',
                                      ' ' 'WAERS'   'ZC102FIT0014' 'C' ' ',
                                      ' ' 'HISTORY' 'ZC102FIT0014' ' ' 'X',
                                      ' ' 'TTYPE'   'ZC102FIT0014' 'C' ' '.
    PERFORM create_object.
    PERFORM register_tree_event.
    PERFORM set_layout.
    PERFORM build_node.


    CALL METHOD go_tree->add_nodes
      EXPORTING
        table_structure_name = 'MTREESNODE'
        node_table           = node_table.

    CALL METHOD go_tree->expand_node
      EXPORTING
        node_key = 'ROOT'.


    SET HANDLER : lcl_event_handler=>hotspot_click_partner FOR go_right_alv_grid.

    CALL METHOD go_right_alv_grid->set_table_for_first_display
      EXPORTING
        is_variant      = gs_variant
        i_save          = 'A'
        i_default       = 'X'
        is_layout       = gs_layout
      CHANGING
        it_outtab       = gt_account
        it_fieldcatalog = gt_fcat.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog  USING  pv_key pv_field pv_table pv_just pv_emph.

  gs_fcat-key       = pv_key.
  gs_fcat-fieldname = pv_field.
  gs_fcat-ref_table = pv_table.
  gs_fcat-just      = pv_just.
  gs_fcat-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'WRBTR'.
      gs_fcat-cfieldname = 'WAERS'.
    WHEN 'PARTNER'.
      gs_fcat-hotspot = abap_true.
  ENDCASE.

  APPEND gs_fcat TO gt_fcat.
  CLEAR gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .

  gs_layout-zebra = abap_true.
  gs_layout-cwidth_opt = 'A'.
  gs_layout-sel_mode   = 'D'.

*  gs_variant-report = sy-repid.
*  gs_variant-handle = 'ALV'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  CREATE OBJECT go_container
    EXPORTING
      repid     = sy-repid
      dynnr     = sy-dynnr
      side      = cl_gui_docking_container=>dock_at_left
      extension = 5000.

  CREATE OBJECT go_base_cont
    EXPORTING
      parent        = go_container
      orientation   = 1
      sash_position = 20
      with_border   = 1.

  go_left_cont  = go_base_cont->top_left_container.
  go_right_cont = go_base_cont->bottom_right_container.
*--------------------------------------------------------------------*
  CREATE OBJECT go_split_cont
    EXPORTING
      parent  = go_right_cont
      rows    = 2
      columns = 1.

  CALL METHOD go_split_cont->get_container
    EXPORTING
      row       = 1
      column    = 1
    RECEIVING
      container = go_right_cont.

  CALL METHOD go_split_cont->get_container
    EXPORTING
      row       = 2
      column    = 1
    RECEIVING
      container = go_right_bot_cont.

  CREATE OBJECT go_tree
    EXPORTING
      parent              = go_left_cont
      node_selection_mode = cl_gui_simple_tree=>node_sel_mode_single.

  CREATE OBJECT go_right_alv_grid
    EXPORTING
      i_parent = go_right_cont.

  CREATE OBJECT go_chart
    EXPORTING
      parent = go_right_bot_cont.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_screen .

  DATA : ls_stable TYPE lvc_s_stbl.

*-- 현재 커서
  ls_stable-row = abap_true.
  ls_stable-col = abap_true.

*-- alv refresh
  CALL METHOD go_right_alv_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form renewal_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM renewal_data .

  CLEAR : gs_account, gs_total.
  REFRESH : gt_account, gt_total.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form search_clicked_node_info
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> NODE_KEY
*&---------------------------------------------------------------------*
FORM search_clicked_node_info  USING pv_node_key.

  CLEAR : gt_total.
  SELECT accno ttype waers hsl01 hsl02 hsl03 hsl04 hsl05 hsl06
         hsl07 hsl08 hsl09 hsl10 hsl11 hsl12
    FROM zvfiaccsum
    INTO CORRESPONDING FIELDS OF TABLE gt_total
   WHERE accno EQ pv_node_key
     AND gjahr EQ pa_gjahr.

  CLEAR gt_account.
  SELECT serno accno dwdate partner wrbtr waers history ttype gjahr
    FROM zc102fit0014
    INTO CORRESPONDING FIELDS OF TABLE gt_account
   WHERE accno EQ pv_node_key
     AND gjahr EQ pa_gjahr
   ORDER BY serno accno dwdate.

  IF gt_account IS INITIAL.
    MESSAGE s004 DISPLAY LIKE 'E'.
    PERFORM refresh_screen.
    EXIT.
  ELSEIF gt_total IS INITIAL.
    MESSAGE s004 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

  PERFORM refresh_screen.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_tree_event
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_tree_event .

  event-eventid = cl_gui_simple_tree=>eventid_node_double_click.
  event-appl_event = 'X'.
  APPEND event TO events.

  CALL METHOD go_tree->set_registered_events
    EXPORTING
      events                    = events
    EXCEPTIONS
      cntl_error                = 1
      cntl_system_error         = 2
      illegal_event_combination = 3
      OTHERS                    = 4.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  SET HANDLER lcl_tree_event_handler=>handle_node_double_click FOR go_tree.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form build_node
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM build_node .

  DATA: node       TYPE mtreesnode,
        lv_partner TYPE zc102mmt0002-partner,
        lv_text    TYPE zc102mmt0002-name1.

  node-node_key   = 'ROOT'.
  node-text       = '거래 정보'.
  node-isfolder   = 'X'.
*  node-n_image    = '@06@'.   " 접은 이미지
*  node-exp_image  = '@07@'.   " 펼친 이미지
  node-n_image    = '@04@'.   " 접은 이미지
  node-exp_image  = '@05@'.   " 펼친 이미지
  APPEND node TO node_table.
  CLEAR node.

  SORT gt_tree BY partner.

  LOOP AT gt_tree INTO gs_tree.
*--------------------------------------------------------------------*
    ON CHANGE OF gs_tree-partner.

      lv_text = gs_tree-name1.

      node-node_key  = gs_tree-partner.
      node-relatkey  = 'ROOT'.
      node-isfolder  = 'X'.
      node-n_image   = '@04@'. " 접은 이미지
      node-exp_image = '@05@'. " 펼친 이미지
      node-text = lv_text.
      APPEND node TO node_table.

      CLEAR node.

    ENDON.
*--------------------------------------------------------------------*
    node-node_key = gs_tree-accno.  " 실질적인 값?
    node-text = gs_tree-accnum.
    node-relatkey = gs_tree-partner.
    node-isfolder = ' '.
    APPEND node TO node_table.
    CLEAR : node.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form fill_tree_master_info
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM fill_tree_master_info .

  SELECT a~partner name1 accnum accno
    INTO CORRESPONDING FIELDS OF TABLE gt_tree
    FROM zc102mmt0002 AS a INNER JOIN zc102fit0013 AS b
      ON a~partner EQ b~partner
    WHERE accno NE ''
      AND accnum NE ''
      AND name1 NE ''
      AND a~partner NE ''.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_double_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_ROW_ID
*&      --> E_COLUMN_ID
*&---------------------------------------------------------------------*
FORM handle_double_click  USING  pv_row_id
                                 pv_column_id.

  READ TABLE gt_account INTO gs_account INDEX pv_row_id.

  CASE gs_account-ttype.
    WHEN 'D'.
      PERFORM get_header_dr. " 입급 D DR
    WHEN 'K'.
      PERFORM get_header_kr. " 출급 K KR
  ENDCASE.

*  SORT gt_migyeol BY belnr ASCENDING.

  CALL SCREEN 110 STARTING AT 50 5
                  ENDING AT 130 20.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_screen_110
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen_110 .

  IF go_pop_cont IS NOT BOUND.

    CLEAR : gt_pop_fcat, gs_pop_fcat.
    PERFORM set_pop_field_catalog USING : 'X' 'BUKRS' 'ZC102FIT0009' 'C' ' ',
                                          'X' 'BELNR' 'ZC102FIT0009' 'C' ' ',
                                          'X' 'GJAHR' 'ZC102FIT0009' 'C' ' ',
                                          ' ' 'BLART' 'ZC102FIT0009' 'C' ' ',
                                          ' ' 'BLDAT' 'ZC102FIT0009' 'C' ' ',
                                          ' ' 'BUDAT' 'ZC102FIT0009' 'C' ' ',
                                          ' ' 'BKTXT' 'ZC102FIT0009' 'C' ' ',
*                                          ' ' 'STBLG' 'ZC102FIT0009' 'C' ' ',
*                                          ' ' 'STODT' 'ZC102FIT0009' 'C' ' ',
*                                          ' ' 'STGRD' 'ZC102FIT0009' 'C' ' ',
                                          ' ' 'PARTNER' 'ZC102FIT0010' 'C' 'X',
                                          ' ' 'WAERS' 'ZC102FIT0009' 'C' ' '.

    PERFORM set_pop_layout.
    PERFORM create_object_110.

    CALL METHOD go_pop_alv_grid->set_table_for_first_display
      EXPORTING
        is_variant      = gs_pop_variant
        i_save          = 'A'
        i_default       = 'X'
        is_layout       = gs_pop_layout
      CHANGING
        it_outtab       = gt_migyeol
        it_fieldcatalog = gt_pop_fcat.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pop_field_catalog
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_pop_field_catalog  USING pv_key pv_field pv_table pv_just pv_emph.

  gs_pop_fcat-key       = pv_key.
  gs_pop_fcat-fieldname = pv_field.
  gs_pop_fcat-ref_table = pv_table.
  gs_pop_fcat-just      = pv_just.
  gs_pop_fcat-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'WRBTR'.
      gs_pop_fcat-cfieldname = 'WAERS'.
  ENDCASE.

  APPEND gs_pop_fcat TO gt_pop_fcat.
  CLEAR gs_pop_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_pop_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_pop_layout .

  gs_pop_layout-zebra      = abap_true.
  gs_pop_layout-cwidth_opt = 'A'.
  gs_pop_layout-sel_mode   = ' '.
  gs_pop_layout-grid_title = '거래처 관련 전표헤더'.

  gs_pop_variant-report = sy-repid.
  gs_pop_variant-handle = 'POPALV1'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object_110
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object_110 .

  CREATE OBJECT go_pop_cont
    EXPORTING
      container_name = 'POP_CONT'.

  CREATE OBJECT go_pop_alv_grid
    EXPORTING
      i_parent = go_pop_cont.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_header_dr
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_header_dr .

  DATA : lv_date TYPE zc102fit0014-dwdate.

  lv_date = gs_account-dwdate(4).

  CLEAR : gt_migyeol.
  SELECT a~bukrs a~belnr a~gjahr blart bldat budat bktxt stblg stodt
*    stgrd
         a~waers partner
    FROM zc102fit0009 AS a INNER JOIN zc102fit0010 AS b
      ON a~bukrs EQ b~bukrs
     AND a~belnr EQ b~belnr
     AND a~gjahr EQ b~gjahr
    INTO CORRESPONDING FIELDS OF TABLE gt_migyeol
   WHERE partner = gs_account-partner
     AND blart = 'DR'
     AND stblg = ''
     AND a~gjahr = lv_date
     AND a~waers = gs_account-waers
    ORDER BY a~bukrs a~belnr a~gjahr.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_header_kr
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_header_kr .

  DATA : lv_date TYPE zc102fit0014-dwdate.

  lv_date = gs_account-dwdate(4).

  CLEAR : gt_migyeol.
  SELECT a~bukrs a~belnr a~gjahr blart bldat budat bktxt stblg stodt
*    stgrd
         a~waers partner
    FROM zc102fit0009 AS a INNER JOIN zc102fit0010 AS b
      ON a~bukrs EQ b~bukrs
     AND a~belnr EQ b~belnr
     AND a~gjahr EQ b~gjahr
    INTO CORRESPONDING FIELDS OF TABLE gt_migyeol
   WHERE partner = gs_account-partner
     AND blart = 'KR'
     AND stblg = ''
     AND a~gjahr = lv_date
     AND a~waers = gs_account-waers
    ORDER BY a~bukrs a~belnr a~gjahr.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_chart
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_chart .

  CLEAR : go_ixml,
          go_ixml_sf,
          go_ixml_docu,
          go_ixml_ostream,
          go_ixml_encoding,
          go_chartdata,
          go_categories,
          go_category,
          go_series,
          go_point,
          go_value.
  CLEAR : gv_xstring.

  go_ixml = cl_ixml=>create(  ).
  go_ixml_sf = go_ixml->create_stream_factory( ).

  go_ixml_docu = go_ixml->create_document( ).

  go_ixml_encoding = go_ixml->create_encoding(
                       byte_order    = if_ixml_encoding=>co_little_endian
                       character_set = 'utf-8' ).

  go_ixml_docu->set_encoding( encoding = go_ixml_encoding ).

*-- Now build a DOM, representing an XML document with chart data
  go_chartdata = go_ixml_docu->create_simple_element(
                                        name   = 'ChartData'
                                        parent = go_ixml_docu ).

*-- Categories (parent)
  go_categories = go_ixml_docu->create_simple_element(
                                        name   = 'Categories'
                                        parent = go_chartdata ).

  PERFORM set_category_value.
  PERFORM set_chart_value.

  go_chart->set_data( xdata = gv_xstring ).
  go_chart->render( ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_category_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_category_value .

  DATA : lv_month TYPE zc102sdt0008-month_sd.

  lv_month = 1.

  DO 12 TIMES.

    go_category = go_ixml_docu->create_simple_element(
                                    name   = 'Category'
                                    parent = go_categories ).
    go_category->if_ixml_node~set_value( lv_month && '월' ).

    lv_month += 1.

  ENDDO.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_chart_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_chart_value .

  DATA : lv_value TYPE string,
         lv_dsum  TYPE zvfiaccsum-hsl01,
         lv_hsum  TYPE zvfiaccsum-hsl01,
         lv_month TYPE bkpf-monat.

  FIELD-SYMBOLS : <ls_value> TYPE any.

  lv_value = '입금'.

*-- Build series (we need only 1)
  go_series = go_ixml_docu->create_simple_element(
                                                   name = 'Series'
                                                    parent = go_chartdata ).
  go_series->set_attribute( name  = 'label'
                            value = lv_value ). "'Series1' ).

  CLEAR gs_total.
  READ TABLE gt_total INTO gs_total WITH KEY ttype = 'D'.

  DO 12 TIMES.

    lv_month = sy-index.
    lv_value = 'HSL' && lv_month.

    ASSIGN COMPONENT lv_value OF STRUCTURE gs_total TO <ls_value>.
    IF <ls_value> IS NOT ASSIGNED.
      EXIT.
    ENDIF.

    lv_value = <ls_value>.

    go_point = go_ixml_docu->create_simple_element(
                                                    name = 'Point'
                                                    parent = go_series ).  " 라벨
    go_point->set_attribute( name  = 'label'
                             value = lv_value ).

    go_value = go_ixml_docu->create_simple_element(
                                                    name = 'Value'
                                                    parent = go_point ). " 막대기 값

    go_value->if_ixml_node~set_value( lv_value ).

    UNASSIGN <ls_value>.

  ENDDO.

  lv_value = '출금'.

*-- Build series (we need only 1)
  go_series = go_ixml_docu->create_simple_element(
                                                   name = 'Series'
                                                    parent = go_chartdata ).
  go_series->set_attribute( name  = 'label'
                            value = lv_value ). "'Series1' ).

  CLEAR gs_total.
  READ TABLE gt_total INTO gs_total WITH KEY ttype = 'W'.

  DO 12 TIMES.

    lv_month = sy-index.
    lv_value = 'HSL' && lv_month.

    ASSIGN COMPONENT lv_value OF STRUCTURE gs_total TO <ls_value>.
    IF <ls_value> IS NOT ASSIGNED.
      EXIT.
    ENDIF.

    lv_value = <ls_value>.

    go_point = go_ixml_docu->create_simple_element(
                                                    name = 'Point'
                                                    parent = go_series ).  " 라벨
    go_point->set_attribute( name  = 'label'
                             value = lv_value ).

    go_value = go_ixml_docu->create_simple_element(
                                                    name = 'Value'
                                                    parent = go_point ). " 막대기 값

    go_value->if_ixml_node~set_value( lv_value ).

    UNASSIGN <ls_value>.

  ENDDO.

  go_ixml_ostream = go_ixml_sf->create_ostream_xstring( gv_xstring ).
  go_ixml_docu->render( go_ixml_ostream ). "here f_xstring is filled

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_init_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_init_value .

  pa_gjahr = sy-datum(4).

ENDFORM.

``` abap
*&---------------------------------------------------------------------*
*& Include          ZC102FIR0021F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form get_base_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_base_data .

*-- BP 데이터
  SELECT a~partner,
         a~name1,
         SUM( coalesce( b~wrbtr, 0 ) ) AS wrbtr,
         waers
    FROM zc102bpt0001 AS a
    LEFT JOIN zc102fit0010 AS b
      ON a~partner = b~partner
       AND b~shkzg = 'S' " 필요 시 여기에 조건 걸기
    INTO CORRESPONDING FIELDS OF TABLE @gt_partner
    WHERE a~partner IN @so_bpart
    GROUP BY a~partner, a~name1, waers
    ORDER BY a~partner.

*-- 미결 전표 헤더
  SELECT a~belnr, partner, a~waers, SUM( wrbtr ) AS wrbtr,
         bktxt, bldat, budat
    FROM zc102fit0010 AS a INNER JOIN zc102fit0009 AS b
      ON a~bukrs = b~bukrs
     AND a~belnr = b~belnr
     AND a~gjahr = b~gjahr
    INTO CORRESPONDING FIELDS OF TABLE @gt_open_h
    WHERE partner IN @so_bpart
      AND shkzg = 'S'
    GROUP BY a~belnr, partner, a~waers, bktxt, bldat, budat, shkzg
    ORDER BY a~belnr.

*-- 미결 전표 아이템
  SELECT belnr, buzei, shkzg, saknr, partner, wrbtr,
         waers, sgtxt
    FROM zc102fit0010
    INTO CORRESPONDING FIELDS OF TABLE @gt_open
    WHERE partner IN @so_bpart
    ORDER BY belnr, shkzg DESCENDING.

*-- 반제 전표 헤더
  SELECT a~belnr, partner, rebzg, a~waers, SUM( wrbtr ) AS wrbtr,
         bktxt, bldat, budat
    FROM zc102fit0016 AS a INNER JOIN zc102fit0009 AS b
      ON a~bukrs = b~bukrs
     AND a~belnr = b~belnr
     AND a~gjahr = b~gjahr
    INTO CORRESPONDING FIELDS OF TABLE @gt_clear_h
    WHERE partner IN @so_bpart
      AND shkzg = 'S'
    GROUP BY a~belnr, partner, a~waers, rebzg, bktxt, bldat, budat
    ORDER BY a~belnr.

*-- 반제 전표 아이템
  SELECT belnr, buzei, shkzg, saknr, wrbtr, waers, sgtxt
    FROM zc102fit0016
    INTO CORRESPONDING FIELDS OF TABLE @gt_clear
    WHERE partner IN @so_bpart
    ORDER BY belnr, shkzg DESCENDING.

  IF gt_partner IS INITIAL.
    MESSAGE s004 DISPLAY LIKE 'E'.
    STOP.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_screen_0100
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen_0100 .

  IF go_tree IS NOT BOUND.

    PERFORM init_tree.
    PERFORM define_hierarchy_header CHANGING gs_hierhdr.
    PERFORM build_comment USING gt_list_commentary gv_logo.
    PERFORM define_field_catalog.
    PERFORM create_hierarchy.
    PERFORM fill_column_tree.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form init_tree
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM init_tree .

  CREATE OBJECT go_container
    EXPORTING
      repid     = sy-repid
      dynnr     = sy-dynnr
      side      = go_container->dock_at_left
      extension = 3000.

  CREATE OBJECT go_tree
    EXPORTING
      parent              = go_container
      node_selection_mode = cl_gui_column_tree=>node_sel_mode_multiple
      item_selection      = 'X'
      no_html_header      = pa_check.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form define_hierarchy_header
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- GS_HIERHDR
*&---------------------------------------------------------------------*
FORM define_hierarchy_header CHANGING pv_hierhdr TYPE treev_hhdr.

  pv_hierhdr-heading = 'Business Partner'.
  pv_hierhdr-tooltip = 'Business Partner'.
  pv_hierhdr-width = 35.
  pv_hierhdr-width_pix = space.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form build_comment
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GT_LIST_COMMENTARY
*&      --> GV_LOGO
*&---------------------------------------------------------------------*
FORM build_comment USING pt_list_commentary TYPE slis_t_listheader
                         pv_logo            TYPE sdydo_value.
  DATA: ls_line TYPE slis_listheader.

  CLEAR ls_line.
  ls_line-typ = 'H'. " High font
  ls_line-info = '비즈니스 파트너 거래내역'.
  APPEND ls_line TO pt_list_commentary.

  CLEAR ls_line.
  ls_line-typ = 'S'. " Small font
  ls_line-key = 'Current date : '.
  ls_line-info = sy-datum.
  APPEND ls_line TO pt_list_commentary.

  CLEAR ls_line.
  ls_line-typ = 'S'.
  ls_line-key = 'Current time : '.
  ls_line-info = sy-uzeit.
  APPEND ls_line TO pt_list_commentary.

*  CLEAR ls_line.
*  ls_line-typ = 'A'. " Italic font
*  ls_line-info = 'SYNC-6 Seoul'.
*  APPEND ls_line TO pt_list_commentary.
*
  pv_logo = 'ENJOYSAP_LOGO'.
  gs_variant-report = sy-repid.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form define_field_catalog
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM define_field_catalog .
*gt_tr_partner.
  CLEAR : gt_fcat, gs_fcat.
  PERFORM set_field_catalog USING :
                                    'BUZEI'  '개별항목'       'X',
                                    'SHKZG'  '차/대 구분'     'X',
                                    'SAKNR'  'G/L Account' 'X',
                                    'WRBTR'  '금액'          ' ',
                                    'WAERS'  '통화키'         ' ',
                                    'SGTXT'  '아이템 상세'      ' ',
                                    'BLDAT'  '증빙 일자'       ' ',
                                    'BUDAT'  '전기 일자'       ' '.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_field_catalog USING pv_field pv_text pv_noout.

  gs_fcat-fieldname = pv_field.
  gs_fcat-coltext   = pv_text.
  gs_fcat-no_out    = pv_noout.
  gs_fcat-outputlen = 36.

  CASE pv_field.
    WHEN 'WRBTR'.
      gs_fcat-cfieldname = 'WAERS'.
  ENDCASE.

  APPEND gs_fcat TO gt_fcat.
  CLEAR gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_hierarchy
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_hierarchy .

*-- 트리 오른쪽 데이터 출력
  CALL METHOD go_tree->set_table_for_first_display
    EXPORTING
      is_variant          = gs_variant
      i_save              = 'A'
      i_default           = 'X'
      is_hierarchy_header = gs_hierhdr
      it_list_commentary  = gt_list_commentary
*     i_logo              = gv_logo
      i_background_id     = 'SIWB_WALLPAPER'  " 'TRVPICTURE18'
    CHANGING
      it_outtab           = gt_tree
      it_fieldcatalog     = gt_fcat.

*--register item_cxt_menu_request------------------------------------*.
  SET HANDLER : lcl_event_handler=>on_item_cxt_menu_request  FOR go_tree,
                lcl_event_handler=>on_item_cxt_menu_selected FOR go_tree,
                lcl_event_handler=>on_checkbox_change        FOR go_tree.

  CALL METHOD go_tree->get_registered_events
    IMPORTING
      events = gt_events.

  gs_event-eventid = cl_gui_column_tree=>eventid_checkbox_change.
  APPEND gs_event TO gt_events.
*  gs_event-eventid = cl_gui_column_tree=>eventid_item_context_menu_req.
*  APPEND gs_event TO gt_events.

  CALL METHOD go_tree->set_registered_events
    EXPORTING
      events = gt_events.

*--add new functions to toolbar--------------------------------------*
*  PERFORM add_function_to_toolbar.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form fill_column_tree
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM fill_column_tree .

  DATA: lv_node_text    TYPE lvc_value,
        lv_bu_group_key TYPE lvc_nkey,
        lv_root_key     TYPE lvc_nkey,
        lv_open_key     TYPE lvc_nkey,
        lv_last_key     TYPE lvc_nkey,
        lv_clear_key    TYPE lvc_nkey,
        lt_layout_item  TYPE lvc_t_layi,
        ls_layout       TYPE lvc_s_layn.

*-- Layout 세팅
  ls_layout-isfolder = 'X'.
  ls_layout-n_image  = '@06@'.
  ls_layout-exp_image = '@07@'.

*-- BP 노드 생성
  LOOP AT gt_partner INTO gs_partner.
    CLEAR gs_tree.
*    gs_tree-partner = gs_partner-partner.
    gs_tree-name1 = gs_partner-name1.
    gs_tree-sgtxt = gs_partner-name1.
    gs_tree-wrbtr = gs_partner-wrbtr.
    gs_tree-waers = gs_partner-waers.
*    APPEND gs_tree TO gt_tree.

    PERFORM create_item_layouts USING abap_false CHANGING lt_layout_item.

    lv_node_text = gs_partner-name1.

    CALL METHOD go_tree->add_node
      EXPORTING
        i_relat_node_key = lv_root_key           " 상위 노드 키
        i_relationship   = cl_gui_column_tree=>relat_last_child
        i_node_text      = lv_node_text
        is_node_layout   = ls_layout
        it_item_layout   = lt_layout_item
        is_outtab_line   = gs_tree               " 위에서 넣은 라인 그대로 넣음
      IMPORTING
        e_new_node_key   = lv_bu_group_key.

*-- 미결 헤더 노드
    LOOP AT gt_open_h INTO gs_open_h WHERE partner = gs_partner-partner.
      CLEAR gs_tree.
      gs_tree-belnr = gs_open_h-belnr.
*      gs_tree-wrbtr = gs_open_h-wrbtr.
*      gs_tree-waers = gs_open_h-waers.
*      gs_tree-bktxt = gs_open_h-bktxt.
      gs_tree-sgtxt = gs_open_h-bktxt.
*-- 날짜를 문자열로 변환 (예: 20250423 -> '2025-04-23')
      gs_tree-bldat = gs_open_h-bldat+0(4) && '-' &&
                      gs_open_h-bldat+4(2) && '-' &&
                      gs_open_h-bldat+6(2).

      gs_tree-budat = gs_open_h-budat+0(4) && '-' &&
                      gs_open_h-budat+4(2) && '-' &&
                      gs_open_h-budat+6(2).

      PERFORM create_item_layouts USING abap_false CHANGING lt_layout_item.

      lv_node_text = gs_open_h-belnr.
      CALL METHOD go_tree->add_node
        EXPORTING
          i_relat_node_key = lv_bu_group_key           " 상위 노드 키
          i_relationship   = cl_gui_column_tree=>relat_last_child
          i_node_text      = lv_node_text
          is_node_layout   = ls_layout
          it_item_layout   = lt_layout_item
          is_outtab_line   = gs_tree               " 위에서 넣은 라인 그대로 넣음
        IMPORTING
          e_new_node_key   = lv_open_key.

*-- 미결 아이템 노드
      LOOP AT gt_open INTO gs_open WHERE belnr = gs_open_h-belnr.
        " 데이터 설치
        CLEAR gs_tree.
*        gs_tree-belnr = gs_open-belnr.
        gs_tree-wrbtr = gs_open-wrbtr.
        gs_tree-waers = gs_open-waers.
        gs_tree-sgtxt = gs_open-sgtxt.
        gs_tree-saknr = gs_open-saknr.
        gs_tree-buzei = gs_open-buzei.
        gs_tree-shkzg = gs_open-shkzg.
*        APPEND gs_tree TO gt_tree.

        PERFORM create_item_layouts USING abap_false CHANGING lt_layout_item.

        IF gs_open-shkzg = 'S'.
          lv_node_text = '차변'.
        ELSE.
          lv_node_text = '대변'.
        ENDIF.
        CALL METHOD go_tree->add_node
          EXPORTING
            i_relat_node_key = lv_open_key           " 상위 노드 키
            i_relationship   = cl_gui_column_tree=>relat_last_child
            i_node_text      = lv_node_text
            it_item_layout   = lt_layout_item
            is_outtab_line   = gs_tree               " 위에서 넣은 라인 그대로 넣음
          IMPORTING
            e_new_node_key   = lv_last_key.
      ENDLOOP.

*-- 반제 헤더 노드
      LOOP AT gt_clear_h INTO gs_clear_h WHERE rebzg = gs_open_h-belnr.
        ON CHANGE OF gs_clear_h-belnr.

          CLEAR gs_tree.
          gs_tree-belnr = gs_clear_h-belnr.
*          gs_tree-wrbtr = gs_clear_h-wrbtr.
*          gs_tree-waers = gs_clear_h-waers.
*          gs_tree-bktxt = gs_clear_h-bktxt.
          gs_tree-sgtxt = gs_clear_h-bktxt.
          gs_tree-bldat = gs_clear_h-bldat+0(4) && '-' &&
                          gs_clear_h-bldat+4(2) && '-' &&
                          gs_clear_h-bldat+6(2).

          gs_tree-budat = gs_clear_h-budat+0(4) && '-' &&
                          gs_clear_h-budat+4(2) && '-' &&
                          gs_clear_h-budat+6(2).
*          APPEND gs_tree TO gt_tree.

          PERFORM create_item_layouts USING abap_false CHANGING lt_layout_item.

          lv_node_text = gs_clear_h-belnr.
          CALL METHOD go_tree->add_node
            EXPORTING
              i_relat_node_key = lv_open_key           " 상위 노드 키
              i_relationship   = cl_gui_column_tree=>relat_last_child
              i_node_text      = lv_node_text
              is_node_layout   = ls_layout
              it_item_layout   = lt_layout_item
              is_outtab_line   = gs_tree               " 위에서 넣은 라인 그대로 넣음
            IMPORTING
              e_new_node_key   = lv_clear_key.
        ENDON.

*-- 반제 아이템 노드
        LOOP AT gt_clear INTO gs_clear WHERE belnr = gs_clear_h-belnr.
          CLEAR gs_tree.
*          gs_tree-belnr = gs_clear-belnr.
          gs_tree-wrbtr = gs_clear-wrbtr.
          gs_tree-waers = gs_clear-waers.
          gs_tree-sgtxt = gs_clear-sgtxt.
          gs_tree-saknr = gs_clear-saknr.
          gs_tree-buzei = gs_clear-buzei.
          gs_tree-shkzg = gs_clear-shkzg.
*          APPEND gs_tree TO gt_tree.

          PERFORM create_item_layouts USING abap_false CHANGING lt_layout_item.

          IF gs_clear-shkzg = 'S'.
            lv_node_text = '차변'.
          ELSE.
            lv_node_text = '대변'.
          ENDIF.
          CALL METHOD go_tree->add_node
            EXPORTING
              i_relat_node_key = lv_clear_key           " 상위 노드 키
              i_relationship   = cl_gui_column_tree=>relat_last_child
              i_node_text      = lv_node_text
              it_item_layout   = lt_layout_item
              is_outtab_line   = gs_tree               " 위에서 넣은 라인 그대로 넣음
            IMPORTING
              e_new_node_key   = lv_last_key.
        ENDLOOP.
      ENDLOOP.
    ENDLOOP.
  ENDLOOP.

  CALL METHOD : go_tree->update_calculations,
                go_tree->frontend_update,
                cl_gui_cfw=>flush.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form change
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_1
*&      --> CL_GUI_COLUMN_TREE=>ITEM_FONT_
*&---------------------------------------------------------------------*
FORM change USING type  TYPE i
                   value TYPE i.

  DATA: lt_selected_nodes   TYPE lvc_t_nkey,
        ls_selected_node    TYPE lvc_nkey,
        lt_item_layout      TYPE lvc_t_layi,
        ls_item_layout_wa   TYPE lvc_s_layi,
        lt_change_layout    TYPE lvc_t_laci,
        ls_change_layout_wa TYPE lvc_s_laci,
        l_fieldname         TYPE lvc_fname,
        ls_outtab           TYPE sflight.

  CALL METHOD go_tree->get_selected_nodes
    CHANGING
      ct_selected_nodes = lt_selected_nodes.

  IF lt_selected_nodes IS INITIAL.
    CALL METHOD go_tree->get_selected_item
      IMPORTING
        e_selected_node = ls_selected_node
        e_fieldname     = l_fieldname.
    APPEND ls_selected_node TO lt_selected_nodes.
  ENDIF.

  LOOP AT lt_selected_nodes INTO ls_selected_node.
    CALL METHOD go_tree->get_outtab_line
      EXPORTING
        i_node_key     = ls_selected_node
      IMPORTING
        e_outtab_line  = ls_outtab
        et_item_layout = lt_item_layout.

    CLEAR lt_change_layout.
    LOOP AT lt_item_layout INTO ls_item_layout_wa.
      IF l_fieldname IS INITIAL OR
         ls_item_layout_wa-fieldname EQ l_fieldname.

        MOVE-CORRESPONDING ls_item_layout_wa TO ls_change_layout_wa.

        CASE type.
          WHEN 0.
            ls_change_layout_wa-class = value.
            ls_change_layout_wa-u_class = 'X'.
            IF value EQ cl_gui_column_tree=>item_class_checkbox.
              ls_change_layout_wa-editable = 'X'.
            ELSE.
              ls_change_layout_wa-editable = space.
            ENDIF.
            ls_change_layout_wa-u_editable = 'X'.
          WHEN 1.
            ls_change_layout_wa-font = value.
            ls_change_layout_wa-u_font = 'X'.
          WHEN 2.
            ls_change_layout_wa-style = value.
            ls_change_layout_wa-u_style = 'X'.
          WHEN 3.
            IF value EQ 0.
              ls_change_layout_wa-t_image = icon_okay.
            ELSE.
              ls_change_layout_wa-t_image = space.
            ENDIF.

            ls_change_layout_wa-u_t_image = 'X'.
        ENDCASE.
        APPEND ls_change_layout_wa TO lt_change_layout.
      ENDIF.
    ENDLOOP.

    CALL METHOD go_tree->change_node
      EXPORTING
        i_node_key     = ls_selected_node
        i_outtab_line  = ls_outtab
        it_item_layout = lt_change_layout.

  ENDLOOP.

  CALL METHOD go_tree->frontend_update.
  CALL METHOD cl_gui_cfw=>flush.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_item_layouts
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- LT_LAYOUT_ITEM
*&---------------------------------------------------------------------*
FORM create_item_layouts USING pv_mode
                         CHANGING pt_item_layout TYPE lvc_t_layi.

  DATA: ls_item_layout TYPE lvc_s_layi.

  CLEAR pt_item_layout.
*  LOOP AT gt_fcat INTO gs_fcat.
*    CLEAR ls_item_layout.
*    IF gs_fcat-no_out EQ space.
*      ls_item_layout-fieldname = gs_fcat-fieldname.
*      APPEND ls_item_layout TO pt_item_layout.
*    ENDIF.
*  ENDLOOP.

  CLEAR ls_item_layout.
  ls_item_layout-fieldname = go_tree->c_hierarchy_column_name.
  ls_item_layout-class = cl_gui_column_tree=>item_class_checkbox.
  ls_item_layout-editable = 'X'.
  ls_item_layout-chosen = pv_mode.
  APPEND ls_item_layout TO pt_item_layout.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_checkbox_change
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> CHECKED
*&      --> FIELDNAME
*&      --> NODE_KEY
*&---------------------------------------------------------------------*
FORM handle_checkbox_change USING pv_checked pv_fieldname pv_node_key.

  DATA : lt_children      TYPE lvc_t_nkey,
         lt_item_layout   TYPE lvc_t_layi,
         lt_item_layout_c TYPE lvc_t_laci,
         ls_layout        TYPE lvc_s_layn,
         ls_layout_c      TYPE lvc_s_lacn,
         ls_tree          LIKE gs_tree,
         lv_mode          TYPE abap_bool.

*-- 선택한 노드 하위 노드 가져오기
  CALL METHOD go_tree->get_subtree
    EXPORTING
      i_node_key       = pv_node_key
    IMPORTING
      et_subtree_nodes = lt_children.


  LOOP AT lt_children INTO DATA(ls_children).

*-- 서브트리 노드 가져오기
    CLEAR : lt_item_layout.
    CALL METHOD go_tree->get_outtab_line
      EXPORTING
        i_node_key     = ls_children
      IMPORTING
        e_outtab_line  = ls_tree
        et_item_layout = lt_item_layout
        es_node_layout = ls_layout.

    CLEAR : lt_item_layout_c.
*    MOVE-CORRESPONDING ls_layout TO ls_layout_c.
    MOVE-CORRESPONDING lt_item_layout TO lt_item_layout_c.

*-- 체크 여부 Layout 세팅
    PERFORM create_item_layouts_laci USING pv_checked
                                           lt_item_layout
                                     CHANGING lt_item_layout_c.

*-- 노드 체크 or 해제로 변경
    CALL METHOD go_tree->change_node
      EXPORTING
        i_node_key     = ls_children
        i_outtab_line  = ls_tree
*       is_node_layout = ls_layout_c
        it_item_layout = lt_item_layout_c.

  ENDLOOP.

  CALL METHOD go_tree->frontend_update.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form add_function_to_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM add_function_to_toolbar .

  DATA: lo_toolbar     TYPE REF TO cl_gui_toolbar,
        lo_style_menu  TYPE REF TO cl_ctmenu,
        lo_image_menu  TYPE REF TO cl_ctmenu,
        lo_change_menu TYPE REF TO cl_ctmenu.

  CALL METHOD go_tree->get_toolbar_object
    IMPORTING
      er_toolbar = lo_toolbar.

  CHECK NOT lo_toolbar IS INITIAL.

*-- Submenu for fonts
  DATA lo_font_menu TYPE REF TO cl_ctmenu.
  CREATE OBJECT lo_font_menu.

  CALL METHOD lo_font_menu->add_function
    EXPORTING
      fcode = 'FONT_DEFAULT'
      text  = 'Default'.

  CALL METHOD lo_font_menu->add_function
    EXPORTING
      fcode = 'PROP'
      text  = 'Prop'.

  CALL METHOD lo_font_menu->add_function
    EXPORTING
      fcode = 'FIXED'
      text  = 'Fixed'.

*-- Submenu for style
  CREATE OBJECT lo_style_menu.

  CALL METHOD lo_style_menu->add_function
    EXPORTING
      fcode = 'STYLE_DEFAULT'
      text  = 'Default'.

  CALL METHOD lo_style_menu->add_function
    EXPORTING
      fcode = 'INHERITED'
      text  = 'Inherited'.

  CALL METHOD lo_style_menu->add_function
    EXPORTING
      fcode = 'INTENSIFIED'
      text  = 'Intensified'.

  CALL METHOD lo_style_menu->add_function
    EXPORTING
      fcode = 'INACTIVE'
      text  = 'Inactive'.

  CALL METHOD lo_style_menu->add_function
    EXPORTING
      fcode = 'CRITICAL'
      text  = 'Critical'.

  CALL METHOD lo_style_menu->add_function
    EXPORTING
      fcode = 'NEGATIVE'
      text  = 'Negative'.

  CALL METHOD lo_style_menu->add_function
    EXPORTING
      fcode = 'POSITIVE'
      text  = 'Positive'.

  CALL METHOD lo_style_menu->add_function
    EXPORTING
      fcode = 'EMPHASIZED'
      text  = 'Emphasized'.

*-- Submenu for class
  DATA lo_class_menu TYPE REF TO cl_ctmenu.
  CREATE OBJECT lo_class_menu.

  CALL METHOD lo_class_menu->add_function
    EXPORTING
      fcode = 'TEXT'
      text  = 'Text'.

  CALL METHOD lo_class_menu->add_function
    EXPORTING
      fcode = 'CHECKBOX'
      text  = 'Checkbox'.

  CALL METHOD lo_class_menu->add_function
    EXPORTING
      fcode = 'BUTTON'
      text  = 'Button'.

  CALL METHOD lo_class_menu->add_function
    EXPORTING
      fcode = 'LINK'
      text  = 'Link'.

*-- Submenu for image/no image
  CREATE OBJECT lo_image_menu.

  CALL METHOD lo_image_menu->add_function
    EXPORTING
      fcode = 'IMAGE'
      text  = 'Image'.

  CALL METHOD lo_image_menu->add_function
    EXPORTING
      fcode = 'NOIMAGE'
      text  = 'No Image'.

*-- Change menu
  CREATE OBJECT lo_change_menu.

  CALL METHOD lo_change_menu->add_submenu
    EXPORTING
      menu = lo_font_menu
      text = 'Font'.

  CALL METHOD lo_change_menu->add_submenu
    EXPORTING
      menu = lo_style_menu
      text = 'Style'.

  CALL METHOD lo_change_menu->add_submenu
    EXPORTING
      menu = lo_class_menu
      text = 'Class'.

  CALL METHOD lo_change_menu->add_submenu
    EXPORTING
      menu = lo_image_menu
      text = 'Image'.

*-- Add separator to toolbar
  CALL METHOD lo_toolbar->add_button
    EXPORTING
      fcode     = ''
      icon      = ''
      butn_type = cntb_btype_sep.

*-- Add change menu to toolbar
  CALL METHOD lo_toolbar->add_button
    EXPORTING
      fcode     = 'CHANGE'
      icon      = ''
      text      = 'Change'
      butn_type = cntb_btype_dropdown
      quickinfo = 'Change selected items'.

  CALL METHOD lo_toolbar->set_static_ctxmenu
    EXPORTING
      fcode   = 'CHANGE'
      ctxmenu = lo_change_menu.

*-- Set handler
  SET HANDLER lcl_event_handler=>on_function_selected FOR lo_toolbar.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_item_layouts_laci
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PV_MODE
*&      <-- PT_ITEM_LAYOUT
*&      <-- TYPE
*&      <-- LVC_T_LACI
*&---------------------------------------------------------------------*
FORM create_item_layouts_laci  USING pv_mode
                                     pt_item_layout TYPE lvc_t_layi
                               CHANGING pt_item_layout_c TYPE lvc_t_laci.

  DATA: ls_item_layout_c TYPE lvc_s_laci.

  LOOP AT pt_item_layout INTO DATA(ls_item_layout).
    MOVE-CORRESPONDING ls_item_layout TO ls_item_layout_c.
    ls_item_layout_c-chosen = pv_mode.
    ls_item_layout_c-u_chosen = abap_true.
    APPEND ls_item_layout_c TO pt_item_layout_c.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form excel_job
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM excel_job .

  DATA : lt_nkey   TYPE lvc_t_chit,
         lv_line   TYPE i,
         lv_answer.

*-- 체크박스 선택 노드 가져오기 (키, 필드)
  CALL METHOD go_tree->get_checked_items
    IMPORTING
      et_checked_items = lt_nkey.

  IF lt_nkey IS INITIAL.
    MESSAGE i015 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

  PERFORM pop_to_confirm USING TEXT-q01 lv_answer.
  CHECK lv_answer = '1'.

*-- 체크박스 선택 아이템 세팅
  CLEAR gt_tree_download.
  LOOP AT lt_nkey INTO DATA(ls_nkey).

    APPEND gt_tree[ ls_nkey-nodekey ] TO gt_tree_download.

  ENDLOOP.

  IF objfile IS INITIAL.
    TRY.
        CREATE OBJECT objfile.
      CATCH cx_root.
    ENDTRY.
  ENDIF.

*-- 윈도우 저장 창 생성
  IF pfolder IS NOT INITIAL.
    initialfolder = pfolder.
  ELSE.
    objfile->get_temp_directory( CHANGING temp_dir = initialfolder
                                 EXCEPTIONS cntl_error = 1
                                            error_no_gui = 2
                                            not_supported_by_gui = 3 ).
  ENDIF.

  objfile->directory_browse( EXPORTING initial_folder = initialfolder
                             CHANGING selected_folder = pickedfolder
                             EXCEPTIONS cntl_error = 1
                                        error_no_gui = 2
                                        not_supported_by_gui = 3 ).

  IF sy-subrc = 0.
    pfolder = pickedfolder.
  ELSE.
    MESSAGE i053 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

  IF pfolder IS INITIAL.
    EXIT.
  ENDIF.

  CALL METHOD objfile->free.
  FREE objfile.

  PERFORM download_excel.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form pop_to_confirm
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> TEXT_Q01
*&      --> LV_ANSWER
*&---------------------------------------------------------------------*
FORM pop_to_confirm USING pv_question pv_answer.

  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      text_question         = pv_question
      text_button_1         = 'Yes'(001)
      text_button_2         = 'No'(002)
      display_cancel_button = 'X'
    IMPORTING
      answer                = pv_answer
    EXCEPTIONS
      text_not_found        = 1
      OTHERS                = 2.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form download_excel
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM download_excel .

  DATA : lv_rc TYPE i.

*-- File name
  CLEAR gv_temp_filename.
  CONCATENATE pfolder '\' TEXT-t01 '_' sy-datum '.xlsx'
         INTO gv_temp_filename.

  gv_form = 'ZEXCEL_PRINT_CL102_FI01'.
  PERFORM download_template   USING gv_form gv_temp_filename.
  PERFORM open_excel_template USING gv_form.
  PERFORM fill_excel_line.

*-- 기본 Sheet 1 세팅
  CALL METHOD OF excel 'SHEETS' = sheet EXPORTING #1 = 1.
  CALL METHOD OF sheet 'SELECT' NO FLUSH.

*-- 모두 출력 후 맨 위로 커서 이동
  CALL METHOD OF excel 'Cells' = cell EXPORTING #1 = 1 #2 = 2.

  CALL METHOD OF cell 'Select'.

  SET PROPERTY OF excel 'VISIBLE' = 1. " 엑셀 데이터 출력

*-- PDF 변환
  PERFORM convert_to_pdf.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form download_template
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GV_FORM
*&      --> GV_TEMP_FILENAME
*&---------------------------------------------------------------------*
FORM download_template USING pv_zform pv_filename.

  DATA : wwwdata_item TYPE wwwdatatab,
         rc           TYPE i.

  gv_file = pv_filename.

  CALL FUNCTION 'WS_FILE_DELETE'
    EXPORTING
      file   = pv_filename
    IMPORTING
      return = rc.

  IF ( rc NE 0 ) AND ( rc NE 1 ).
    MESSAGE s000 WITH '임시파일 초기화 실패.' '이전에 Excel에서 자료를 Open하였는지 확인.'
                 DISPLAY LIKE 'E'.
  ENDIF.

  SELECT SINGLE *
    INTO CORRESPONDING FIELDS OF wwwdata_item
    FROM wwwdata
    WHERE objid = pv_zform.

  CALL FUNCTION 'DOWNLOAD_WEB_OBJECT'
    EXPORTING
      key         = wwwdata_item
      destination = gv_file.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form open_excel_template
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> GV_FORM
*&---------------------------------------------------------------------*
FORM open_excel_template USING pv_form.

  IF excel IS INITIAL.
    CREATE OBJECT excel 'EXCEL.APPLICATION'.
  ENDIF.

  IF sy-subrc NE 0.
    MESSAGE i001 WITH sy-msgli.
  ENDIF.

  CALL METHOD OF excel 'WORKBOOKS' = workbook.
  SET PROPERTY OF excel 'VISIBLE' = 0.

  CALL METHOD OF workbook 'OPEN' EXPORTING #1 = gv_file.

*-- Sheet 설정
  GET PROPERTY OF : workbook    'Application' = application,
                    application 'ActiveSheet' = activesheet.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form fill_excel_line
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM fill_excel_line .

  DATA : lv_row TYPE i VALUE 2,
         lv_col TYPE i VALUE 1.
  FIELD-SYMBOLS : <fs_field> TYPE any.

  LOOP AT gt_tree_download INTO DATA(ls_tree).

    DO.
      ASSIGN COMPONENT sy-index OF STRUCTURE ls_tree TO <fs_field>.
      IF <fs_field> IS NOT ASSIGNED.
        EXIT.
      ENDIF.
      PERFORM fill_cells USING lv_row sy-index <fs_field>.
      UNASSIGN <fs_field>.
    ENDDO.

    lv_row += 1.
  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form fill_cells
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_10
*&      --> P_3
*&      --> GS_TREE_PARTNER
*&---------------------------------------------------------------------*
FORM fill_cells  USING i j val.

  CALL METHOD OF excel 'CELLS' = cell
    EXPORTING
      #1 = i  " 행
      #2 = j. " 열

  SET PROPERTY OF cell 'VALUE' = val.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form convert_to_pdf
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM convert_to_pdf .

  DATA lv_rc TYPE i.

  CLEAR gv_temp_filename_pdf.
  CONCATENATE pfolder '\' TEXT-t01 '_' sy-datum '.PDF'
              INTO gv_temp_filename_pdf.

  GET PROPERTY OF excel 'Workbooks' = workbook
    EXPORTING #1 = 1.

  CALL METHOD OF workbook 'ExportAsFixedFormat'
    EXPORTING
      #1 = '0'
      #2 = gv_temp_filename_pdf.

  CALL METHOD OF workbook 'Close'
    EXPORTING
      #1 = 0.

  CALL METHOD OF excel 'Quit'.

  CALL METHOD cl_gui_frontend_services=>file_delete
    EXPORTING
      filename = CONV #( gv_temp_filename )
    CHANGING
      rc       = lv_rc.

*-- PDF 가로로 조정 및 Excel 파일 사이즈에 맞게 조절
  SET PROPERTY OF pagesetup 'Orientation' = 2.  " 2 = Landscape, 1 = Portrait
  GET PROPERTY OF excel 'Workbooks' = workbook EXPORTING #1 = 1.
  GET PROPERTY OF workbook 'Worksheets' = worksheet EXPORTING #1 = 1.
  " 열 너비 자동 조정
  GET PROPERTY OF worksheet 'Columns' = columns.
  CALL METHOD OF columns 'AutoFit'.
  GET PROPERTY OF worksheet 'PageSetup' = pagesetup.

  " 인쇄 비율을 한 페이지에 맞추기
  SET PROPERTY OF pagesetup 'Zoom' = 0.
  SET PROPERTY OF pagesetup 'FitToPagesWide' = 1.
  SET PROPERTY OF pagesetup 'FitToPagesTall' = 1.

  CALL METHOD OF workbook 'ExportAsFixedFormat'
    EXPORTING
      #1 = 0
      #2 = gv_temp_filename_pdf.

ENDFORM.

``` abap
*&---------------------------------------------------------------------*
*& Include          ZC102FIR0012F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form set_init_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_init_value .

  pa_bukrs = '1000'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_init_screen .

  LOOP AT SCREEN.

    IF screen-name = 'PA_BUKRS'.
      screen-input = 0.
    ENDIF.

    MODIFY SCREEN.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_base_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_base_data .

  DATA : lv_koart TYPE zc102fit0010-koart,
         lv_dbcnt TYPE sy-dbcnt.

  CLEAR gt_trade.

  CASE pa_btype.
    WHEN 'C'.
      lv_koart = 'D'.
    WHEN 'V'.
      lv_koart = 'K'.
    WHEN OTHERS.
  ENDCASE.


  CASE 'X'.
*-- 미결만 조회
    WHEN pa_open.
      PERFORM get_open_item USING lv_koart.
*-- 반제만 조회
    WHEN pa_clear.
*-- 반제된 미결전표 조회
      PERFORM get_open_clear_item USING lv_koart.
*-- 반제된 미결전표의 반제 전표 조회
      PERFORM get_clear_only_item USING lv_koart.
*-- 전체 조회
    WHEN OTHERS.
      PERFORM get_open_item USING lv_koart.
      PERFORM get_clear_item USING lv_koart.
  ENDCASE.

  lv_dbcnt = lines( gt_trade ).
  IF lv_dbcnt = 0.
    MESSAGE s004 DISPLAY LIKE 'E'.
    STOP.
  ENDIF.

  IF pa_maxr > 0.
    DELETE gt_trade FROM pa_maxr + 1 TO lines( gt_trade ).
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_all_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM  get_open_item USING pv_koart.

  SELECT a~belnr a~gjahr bktxt blart bldat b~duedt
         wrbtr a~waers augbl
  INTO CORRESPONDING FIELDS OF TABLE gt_trade
  FROM zc102fit0009 AS a INNER JOIN zc102fit0010 AS b
    ON a~bukrs = b~bukrs
   AND a~belnr = b~belnr
   AND a~gjahr = b~gjahr
  WHERE partner = pa_bpart
    AND koart = pv_koart
    AND augbl = ''
    AND a~bukrs = pa_bukrs
    AND a~budat IN so_budat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_screen_0100
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen_0100 .

  IF go_container IS NOT BOUND.

    CLEAR : gt_fcat, gs_fcat.
    PERFORM set_fcat USING : 'X' 'STATUS'      ' '            'C' ' ',
                             'X' 'BELNR'       'ZC102FIT0009' 'C' ' ',
                             'X' 'GJAHR'       'ZC102FIT0009' 'C' ' ',
                             ' ' 'BKTXT'       'ZC102FIT0009' ' ' 'X',
                             ' ' 'BLART'       'ZC102FIT0009' 'C' ' ',
                             ' ' 'BLDAT'       'ZC102FIT0009' 'C' ' ',
                             ' ' 'DUEDT'       'ZC102FIT0010' 'C' ' ',
*                             ' ' 'DSTAT'  ' '            'C' 'X',
                             ' ' 'WRBTR'       'ZC102FIT0010' ' ' ' ',
                             ' ' 'WAERS'       'ZC102FIT0010' 'C' 'X',
                             ' ' 'AUGBL'       'ZC102FIT0010' 'C' ' ',
                             ' ' 'SGTXT'       'ZC102FIT0010' ' ' 'X',
                             ' ' 'REBZG'       'ZC102FIT0016' ' ' ' ',
                             ' ' 'REMAIN_DATE' ' '            'C' 'X',
                             ' ' 'CALC_DATE'   ' '            'C' 'X'.

    PERFORM set_layout.
    PERFORM create_object.

    SET HANDLER : lcl_event_handler=>hotspot_click FOR go_alv_grid.
    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        is_variant      = gs_variant
        i_save          = 'A'
        i_default       = 'X'
        is_layout       = gs_layout
      CHANGING
        it_outtab       = gt_trade
        it_fieldcatalog = gt_fcat
        it_sort         = gt_sort.


  ENDIF.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_fcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_fcat USING pv_key pv_field pv_table pv_just pv_emph.

  gs_fcat-key       = pv_key.
  gs_fcat-fieldname = pv_field.
  gs_fcat-ref_table = pv_table.
  gs_fcat-just      = pv_just.
  gs_fcat-emphasize = pv_emph.

  CASE pv_field.
    WHEN 'BELNR'.
      gs_fcat-hotspot = abap_true.
    WHEN 'WRBTR'.
      gs_fcat-cfieldname = 'WAERS'.
      gs_fcat-do_sum = abap_true.
    WHEN 'STATUS'.
      gs_fcat-coltext = '반제 상태'.
    WHEN 'REMAIN_DATE'.
      gs_fcat-coltext = '남은 납기일'.
      CASE 'X'.
        WHEN pa_clear.
          gs_fcat-no_out = abap_true.
      ENDCASE.
*      gs_fcat-no_zero = abap_true.
    WHEN 'CALC_DATE'.

      gs_fcat-no_out = abap_true.

    WHEN 'REBZG'.
      gs_fcat-no_out = abap_true.
  ENDCASE.

  APPEND gs_fcat TO gt_fcat.
  CLEAR gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .

  gs_layout-zebra      = abap_true.
  gs_layout-cwidth_opt = 'A'.
  gs_layout-sel_mode   = 'D'.
  gs_layout-ctab_fname = 'COLOR'.

  gs_variant-report  = sy-repid.
  gs_variant-variant = pa_layo.
  gs_variant-handle  = 'ALV1'.

  CLEAR : gt_sort, gs_sort.
  CASE 'X'.
    WHEN pa_open.
      PERFORM set_sort USING 1 'CALC_DATE' abap_false abap_false.
*-- 반제 선택 시
    WHEN pa_clear.
      PERFORM set_sort USING 1 'REBZG' abap_true abap_true.

*-- 전체 선택시
    WHEN OTHERS.
*-- SET SORT
      PERFORM set_sort USING 1 'STATUS' abap_true abap_true.
      PERFORM set_sort USING 2 'CALC_DATE' abap_false abap_false.
  ENDCASE.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  CREATE OBJECT go_container
    EXPORTING
      container_name = 'MAIN_CONT'.

  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_container.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_screen_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_screen_data .

  DATA : lv_tabix TYPE sy-tabix,
         lv_date  TYPE p,
         ls_scol  TYPE lvc_s_scol.

*-- 헤더값 변경
  gs_partner-partner = pa_bpart.

*-- 여신 한도 및 사용 한도에 따른 상태 관리
  IF gs_partner-total >= gs_partner-used.
    gs_partner-status = icon_led_green.
  ELSE.
    gs_partner-status = icon_led_red.
  ENDIF.

*-- 아이콘 및 지급 상태 상태 변경
  LOOP AT gt_trade INTO gs_trade.

    lv_tabix = sy-tabix.

*-- 반제된 미결전표 번호 = 미결전표 원전표 번호 세팅
    IF gs_trade-rebzg IS INITIAL.
      gs_trade-rebzg = gs_trade-belnr.
    ENDIF.

*-- 헤더면 BKTXT를 아이템 상세로
    IF gs_trade-sgtxt IS INITIAL.
      gs_trade-sgtxt = gs_trade-bktxt.
    ENDIF.

*-- 남은 지급 기한에 따른 색 조정
    " 7일 전 분홍색, 당일 주황색, 지나면 빨간색
    CLEAR lv_date.
    IF gs_trade-duedt IS NOT INITIAL.
      lv_date = gs_trade-duedt - sy-datum.

      IF lv_date = 1.
        PERFORM set_color USING ' ' 7 1 1 ' '
                          CHANGING ls_scol.
      ELSEIF lv_date = 0.
        PERFORM set_color USING ' ' 3 1 1 ' '
                          CHANGING ls_scol.
      ELSEIF lv_date < 0.
        PERFORM set_color USING ' ' 6 1 1 ' '
                          CHANGING ls_scol.
      ELSEIF lv_date <= 7.
        PERFORM set_color USING ' ' 1 1 1 ' '
                          CHANGING ls_scol.
      ENDIF.

      gs_trade-calc_date = lv_date.
      gs_trade-remain_date = lv_date.
    ENDIF.

*-- 아이콘 변경
    IF gs_trade-augbl IS INITIAL.
      gs_trade-status = icon_led_red.
    ELSE.
      gs_trade-status = icon_led_green.
    ENDIF.

    MODIFY gt_trade FROM gs_trade INDEX lv_tabix
                                  TRANSPORTING status color
                                               remain_date calc_date
                                               rebzg sgtxt.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_partner_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_partner_data .

  CLEAR gs_partner.
  gs_partner-bukrs = pa_bukrs.
  CASE pa_btype.
    WHEN 'C'.
      PERFORM get_customer_data.
    WHEN 'V'.
      PERFORM get_vender_data.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_customer_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_customer_data .

  SELECT SINGLE a~partner a~name1 bp_role a~cusno as bpno stras
                b~credit_limit AS total d~credit_used AS used waers
  INTO CORRESPONDING FIELDS OF gs_partner
  FROM zc102bpt0001 AS a INNER JOIN zc102fit0008 AS b
                                 ON a~partner = b~partner
                         INNER  JOIN zc102sdt0001 AS c
                                 ON a~cusno = c~cusno
                         LEFT   JOIN zvficreditc102 AS d
                                 ON a~partner = d~partner
  WHERE a~partner = pa_bpart.

  gs_partner-waers_used    = gs_partner-waers.
  gs_partner-waers_remain  = gs_partner-waers.
  gs_partner-remain        = gs_partner-total -
                             gs_partner-used.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_clear_item
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LV_KOART
*&---------------------------------------------------------------------*
FORM get_clear_item USING pv_koart.

  SELECT a~belnr a~gjahr bktxt blart bldat duedt
         wrbtr a~waers augbl sgtxt
  APPENDING CORRESPONDING FIELDS OF TABLE gt_trade
  FROM zc102fit0009 AS a INNER JOIN zc102fit0016 AS b
    ON a~bukrs = b~bukrs
   AND a~belnr = b~belnr
   AND a~gjahr = b~gjahr
  WHERE partner = pa_bpart
    AND koart = pv_koart
    AND shkzg = 'S'
    AND a~bukrs = pa_bukrs
    AND a~budat IN so_budat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_clear_only_item
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LV_KOART
*&---------------------------------------------------------------------*
FORM get_clear_only_item USING pv_koart.

  SELECT a~belnr, a~gjahr, a~bktxt, a~blart, a~bldat,
         b~wrbtr, a~waers, b~augbl, b~rebzg
    FROM zc102fit0009 AS a INNER JOIN zc102fit0016 AS b
                                   ON a~bukrs = b~bukrs
                           INNER JOIN @gt_trade AS c
                                   ON b~rebzg = c~belnr
     AND a~belnr = b~belnr
     AND a~gjahr = b~gjahr
    WHERE shkzg = 'S'
  APPENDING CORRESPONDING FIELDS OF TABLE @gt_trade.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_open_clear_item
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LV_KOART
*&---------------------------------------------------------------------*
FORM get_open_clear_item USING pv_koart.

  SELECT a~belnr a~gjahr bktxt blart bldat duedt
        SUM( b~wrbtr ) AS wrbtr a~waers augbl
    INTO CORRESPONDING FIELDS OF TABLE gt_trade
    FROM zc102fit0009 AS a INNER JOIN zc102fit0010 AS b
      ON a~bukrs = b~bukrs
     AND a~belnr = b~belnr
     AND a~gjahr = b~gjahr
    WHERE partner = pa_bpart
      AND koart = pv_koart
*      AND shkzg = 'H'
      AND augbl NE ''
      AND a~bukrs = pa_bukrs
      AND a~budat IN so_budat
    GROUP BY a~belnr a~gjahr bktxt blart bldat duedt a~waers augbl.

*  CLEAR gt_store_belnr.
*  MOVE-CORRESPONDING gt_trade TO gt_store_belnr.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_color
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_7
*&      --> P_1
*&      --> P_1
*&      --> P_
*&      <-- LS_SCOL
*&---------------------------------------------------------------------*
FORM set_color USING pv_field pv_color pv_int pv_inv pv_nokey
               CHANGING ps_scol TYPE lvc_s_scol.

  ps_scol-fname     = pv_field.
  ps_scol-color-col = pv_color.
  ps_scol-color-int = pv_int.
  ps_scol-color-inv = pv_inv.
  ps_scol-nokeycol  = pv_nokey.
  INSERT ps_scol INTO TABLE gs_trade-color.
  CLEAR ps_scol.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_sort
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_1
*&      --> P_
*&      --> ABAP_TRUE
*&      --> ABAP_TRUE
*&---------------------------------------------------------------------*
FORM set_sort USING pv_spos pv_field pv_down pv_subtot.

  gs_sort-spos      = pv_spos.
  gs_sort-fieldname = pv_field.
  gs_sort-down      = pv_down.
  gs_sort-subtot    = pv_subtot.
  APPEND gs_sort TO gt_sort.
  CLEAR gs_sort.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_hotspot_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_COLUMN_ID
*&      --> E_ROW_ID
*&---------------------------------------------------------------------*
FORM handle_hotspot_click USING pv_column_id pv_row_id.

  RANGES : lr_gjahr FOR zc102fit0009-gjahr,
           lr_belnr FOR zc102fit0009-belnr.

  CLEAR gs_trade.
  READ TABLE gt_trade INTO gs_trade INDEX pv_row_id.

  IF sy-subrc = 0.

    CASE pv_column_id.
      WHEN 'BELNR'.

        REFRESH lr_gjahr.
        CLEAR lr_gjahr.
        lr_gjahr-sign   = 'I'.
        lr_gjahr-option = 'EQ'.
        lr_gjahr-low    = gs_trade-gjahr.
        APPEND lr_gjahr.

        REFRESH lr_belnr.
        CLEAR lr_belnr.
        lr_belnr-sign   = 'I'.
        lr_belnr-option = 'EQ'.
        lr_belnr-low    = gs_trade-belnr.
        APPEND lr_belnr.

        SUBMIT zc102fir0003 WITH so_gjahr IN lr_gjahr
                            WITH so_belnr IN lr_belnr
                            AND RETURN.

    ENDCASE.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form leave_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM leave_screen .

  CALL METHOD : go_alv_grid->free, go_container->free.

  FREE : go_alv_grid, go_container.

  LEAVE TO SCREEN 0.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_vender_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_vender_data .

  SELECT SINGLE a~partner a~name1 bp_role a~venno as bpno stras
                total_payable AS total d~need_payable AS used waers
  INTO CORRESPONDING FIELDS OF gs_partner
  FROM zc102bpt0001 AS a  INNER JOIN zc102mmt0002 AS c
                                  ON a~venno = c~venno
                          LEFT  JOIN zvfiapsumc102 AS d
                                  ON a~partner = d~partner
  WHERE a~partner = pa_bpart.

  IF gs_partner IS INITIAL.
    MESSAGE s004 DISPLAY LIKE 'E'.
    STOP.
  ENDIF.

  gs_partner-waers_used    = gs_partner-waers.
  gs_partner-waers_remain  = gs_partner-waers.
  gs_partner-remain        = gs_partner-total -
                             gs_partner-used.

ENDFORM.
